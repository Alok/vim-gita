Describe gita#features#reset
  Before
    let gita = gita#get()
    let operations_saved = deepcopy(gita.operations)

    function! gita.operations.reset(...) abort
      let self._previous_args = a:000
    endfunction
    let gita.operations._previous_args = []
  End
  After
    let gita.operations = operations_saved
  End

  Context #exec([{options}, {config}])
    It should call 'gita.operations.reset' internally
      call gita#features#reset#exec({
            \ 'q': 1, 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \ 'N': 1,
            \ 'hard': 1,
            \ 'merge': 1,
            \ 'keep': 1,
            \ 'commit': 1,
            \})
      Assert Equals(gita.operations._previous_args, [{
            \ 'q': 1, 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \ 'N': 1,
            \ 'hard': 1,
            \ 'merge': 1,
            \ 'keep': 1,
            \ 'commit': 1,
            \}, {}])
    End

    It should remove unknown options internally
      call gita#features#reset#exec({
            \ 'unknown_option': 1,
            \})
      Assert Equals(gita.operations._previous_args, [{}, {}])
    End
  End

  Context #command({bang}, {range}[, {cmdline}])
    Before
      let default_options_saved = g:gita#features#reset#default_options
      let g:gita#features#reset#default_options = {}
    End
    After
      let g:gita#features#reset#default_options = default_options_saved
    End

    It should automatically specify 'quiet = 1'
      call gita#features#reset#command('', [0, 0], '')
      Assert Equals(gita.operations._previous_args, [{
            \ '--': [],
            \ 'quiet': 1,
            \}, {}])
    End
    It should automatically expand '%' to absolute path
      call gita#features#reset#command('', [0, 0], '-- %')
      Assert Equals(gita.operations._previous_args, [{
            \ '--': [gita#utils#ensure_abspath(gita#utils#expand('%'))],
            \ 'quiet': 1,
            \}, {}])
    End
    It should automatically add default options
      let g:gita#features#reset#default_options = {
            \ 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \}
      call gita#features#reset#command('', [0, 0])
      Assert Equals(gita.operations._previous_args, [{
            \ '--': [],
            \ 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \}, {}])
    End
  End

  Context #complete({arglead}, {cmdline}, {cursorpos})
    It should call ArgumentParser.complete function
      Assert True(exists('*gita#features#reset#complete'))
      Skip It is not possible to access script local variable
    End
  End
End

